import java.time.LocalDate
import java.time.LocalTime

plugins {
  id "idea"
  id "java"
  id "jacoco"
  id "application"
  id "net.ltgt.apt" version "0.15"
  id "org.flywaydb.flyway" version "5.0.7"
  id "com.diffplug.gradle.spotless" version "3.10.0"
  id "org.springframework.boot" version "2.0.1.RELEASE"
  id "io.spring.dependency-management" version "1.0.5.RELEASE"
}

version = "0.0.1"
group = "com.trevorgowing"
sourceCompatibility = 1.8
targetCompatibility = 1.8
archivesBaseName = "project-log-server"
mainClassName = "com.trevorgowing.projectlog.Application"

spotless {
  java {
    googleJavaFormat()
  }
}

springBoot {
  buildInfo()
}

flyway {
  baselineVersion = "2017.03.19.18.14.33.897"
  outOfOrder = true
}

jacocoTestReport {
  reports {
    xml.enabled = true
    html.enabled = false
  }
}

test {
  testLogging {
    events "failed"
    exceptionFormat "full"
  }
}

task resolveDependencies {
  description = "Download and cache dependencies"
  doLast {
    configurations.compileOnly.resolve()
    configurations.runtime.resolve()
  }
}

task createSqlMigration {
  description = "Create a flyway sql migration in the default location, with a version of timestamp now and default description."
  doLast {
    String version = LocalDate.now().toString().replace('-', '.').concat('.')
        .concat(LocalTime.now().toString().replace(':', '.'))
    String fileName = 'V'.concat(version).concat('__').concat("description").concat(".sql")
    String filePath = "src/main/resources/db/migration/".concat(fileName)
    new File(filePath).createNewFile() ? println(filePath.concat(" migration created successfully"))
        : println("Failed to create migration ".concat(filePath))

  }
}

repositories {
  jcenter()
}

dependencies {
  compileOnly "org.projectlombok:lombok:1.18.0"
  annotationProcessor "org.projectlombok:lombok:1.18.0"

  compile 'com.trevorgowing:url-string-builder:1.1.0'

  compile "org.flywaydb:flyway-core:5.0.7"
  compile "mysql:mysql-connector-java:8.0.11"

  compile "org.springframework.boot:spring-boot-devtools"
  compile "org.springframework.boot:spring-boot-starter-web"
  compile "org.springframework.boot:spring-boot-starter-json"
  compile "org.springframework.boot:spring-boot-starter-data-jpa"
  compile "org.springframework.boot:spring-boot-starter-actuator"

  testCompileOnly "org.projectlombok:lombok:1.18.0"
  testAnnotationProcessor "org.projectlombok:lombok:1.18.0"

  testCompile "io.rest-assured:rest-assured:3.1.0"
  testCompile "io.rest-assured:spring-mock-mvc:3.1.0"
  testCompile "org.springframework.boot:spring-boot-starter-test"

  testRuntime "com.h2database:h2:1.4.197"
}